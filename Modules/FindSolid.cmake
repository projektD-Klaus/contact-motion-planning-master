include(FindPackageHandleStandardArgs)

file(
	GLOB
	SOLID_INCLUDE_PATHS
	$ENV{SOLIDDIR}/include
	$ENV{HOME}/include
	/usr/local/include
	/usr/include
	$ENV{ProgramW6432}/solid*/include
	$ENV{ProgramFiles}/solid*/include
)

file(
	GLOB
	SOLID_LIBRARY_PATHS
	$ENV{SOLIDDIR}/lib
	$ENV{HOME}/lib
	/usr/local/lib
	/usr/lib
	$ENV{ProgramW6432}/solid*/lib
	$ENV{ProgramFiles}/solid*/lib
)

find_path(
	SOLID_INCLUDE_DIRS
	NAMES
	SOLID/SOLID.h
	HINTS
	${SOLID_INCLUDE_PATHS}
)

find_library(
	SOLID_LIBRARY_DEBUG
	NAMES
	solidd solidsd
	HINTS
	${SOLID_LIBRARY_PATHS}
)

find_library(
	SOLID_LIBRARY_RELEASE
	NAMES
	solid solids
	HINTS
	${SOLID_LIBRARY_PATHS}
)

if(SOLID_LIBRARY_DEBUG AND NOT SOLID_LIBRARY_RELEASE)
	set(SOLID_LIBRARIES ${SOLID_LIBRARY_DEBUG})
endif(SOLID_LIBRARY_DEBUG AND NOT SOLID_LIBRARY_RELEASE)

if(SOLID_LIBRARY_RELEASE AND NOT SOLID_LIBRARY_DEBUG)
	set(SOLID_LIBRARIES ${SOLID_LIBRARY_RELEASE})
endif(SOLID_LIBRARY_RELEASE AND NOT SOLID_LIBRARY_DEBUG)

if(SOLID_LIBRARY_DEBUG AND SOLID_LIBRARY_RELEASE)
	set(SOLID_LIBRARIES debug ${SOLID_LIBRARY_DEBUG} optimized ${SOLID_LIBRARY_RELEASE})
endif(SOLID_LIBRARY_DEBUG AND SOLID_LIBRARY_RELEASE)

find_package_handle_standard_args(
	SOLID
	DEFAULT_MSG
	SOLID_INCLUDE_DIRS
	SOLID_LIBRARIES
)

mark_as_advanced(
	SOLID_INCLUDE_DIRS
	SOLID_LIBRARIES
	SOLID_LIBRARY_DEBUG
	SOLID_LIBRARY_RELEASE
) 
